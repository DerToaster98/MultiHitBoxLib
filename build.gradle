plugins {
    id 'net.minecraftforge.gradle' version "${forgegradle_version}"
    id 'org.spongepowered.mixin' version "${mixingradle_version}"
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'org.parchmentmc.librarian.forgegradle' version "${parchmentmc_version}"
    id 'com.github.johnrengelman.shadow' version "${shadow_version}"
}

version = "${mc_version}-${mod_version}"
group = mod_group

base {
    archivesName = modid
}

configurations {
    shade
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mappings_channel, version: mappings_version

    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=${modid}.mixins.json"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            jvmArgs "-XX:+AllowEnhancedClassRedefinition", "-XX:HotswapAgent=fatjar"
            mods {
                "${modid}" {
                    source sourceSets.main
                }
            }
        }

        client {
            taskName 'runClient MHLib 1.20.1'

            property 'forge.enabledGameTestNamespaces', modid
        }

        server {
            taskName 'runServer MHLib 1.20.1'

            property 'forge.enabledGameTestNamespaces', modid
        }
        gameTestServer {
            taskName 'runTestServer MHLib 1.20.1'

            property 'forge.enabledGameTestNamespaces', modid
        }

        data {
            workingDirectory project.file('run-data')

            taskName 'runData MHLib 1.20.1'

            args '--mod', modid, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "CurseMaven Maven"
        url = "https://www.cursemaven.com"
    }

    maven {
        name = "AzureDoom Maven"
        url = "https://libs.azuredoom.com:4443/mods"
    }
	
    maven {
        name = "Geckolib Maven"
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven"
    }

	maven {
        name = "CubicInterpolation Maven"
        url = "https://cubicinterpolation.net/maven/"
    }

    maven {
        name = "BlameJared maven"
        url = "https://maven.blamejared.com/"
    }

    maven {
        name = "RyanLiptak Maven"
        url = "https://maven.ryanliptak.com/"
    }
}

dependencies {
    // Base
    minecraft("net.minecraftforge:forge:${mc_version}-${forge_version}")
    annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")

    // Dependencies (Embedded)
    implementation fg.deobf("commoble.databuddy:${databuddy_branch}:${databuddy_version}")

    shade fg.deobf("commoble.databuddy:${databuddy_branch}:${databuddy_version}")

    // Dependencies (Optional)
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-${geckolib_version}")

    implementation fg.deobf("mod.azure.azurelib:azurelib-forge-${azurelib_version}")

    compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}")

    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")

    implementation fg.deobf("curse.maven:just-enough-effect-descriptions-jeed-532286:${jeed_version}")

    implementation fg.deobf("curse.maven:catalogue-459701:${catalogue_version}")
    implementation fg.deobf("curse.maven:gamemenumodoption-353051:${gmmo_version}")

    implementation fg.deobf("curse.maven:jade-324717:${jade_version}")

    compileOnly fg.deobf("squeek.appleskin:appleskin-forge:${appleskin_version}:api")
    runtimeOnly fg.deobf("squeek.appleskin:appleskin-forge:${appleskin_version}")
}

shadowJar {
	archiveClassifier = ''
	configurations = [project.configurations.shade]
	relocate 'commoble.databuddy', "${mod_group}.shadow.commoble.databuddy"
}

reobf {
    shadowJar { }
}

createMcpToSrg {
    outputs.upToDateWhen {false}

    doLast {
        fixFG5TsrgForMixinAP(output.get().asFile, file("${buildDir}/fixMcpToSrg/output.tsrg"))
    }
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
    config "${modid}.mixins.json"

    reobfSrgFile = file("${buildDir}/fixMcpToSrg/output.tsrg")

    debug.verbose = true
    debug.export = true
    debug = true
}

tasks.build.dependsOn reobfShadowJar

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            mc_version: mc_version, mc_version_range: mc_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version: loader_version, mod_loader: mod_loader,
            modid: modid, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_credits: mod_credits, mod_description: mod_description, mod_issueTrackerURL: mod_issueTrackerURL,
            geckolib_version_range: geckolib_version_range, azurelib_version_range: azurelib_version_range,
            jei_version_range: jei_version_range, jeed_version_range: jeed_version_range,
            catalogue_version_range: catalogue_version_range, gmmo_version_range: gmmo_version_range,
            jade_version_range: jade_version_range, appleskin_version_range: appleskin_version_range
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_credits,
                'Specification-Version'   : mod_version,
                'Implementation-Title'    : mod_name,
                'Implementation-Version'  : mod_version,
                'Implementation-Vendor'   : mod_credits,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs'		  	  : "${modid}.mixins.json"
        ])
    }
    finalizedBy('reobfJar')
    finalizedBy('reobfShadowJar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

// Function that actually fixes the TSRG file
static def fixFG5TsrgForMixinAP(File inFile, File outFile) {
    // Make directory if needed
    outFile.parentFile.mkdirs()

    try (Scanner scanner = new Scanner(inFile); PrintWriter out = new PrintWriter(outFile)) {
        boolean firstLine = true

        while (scanner.hasNextLine()) {
            String next = scanner.nextLine()

            // Skip first 'tsrg left right' header line
            if (firstLine) {
                firstLine = false
                continue
            }

            // Skip 'static' indicators
            if (next.trim() == "static") continue

            // Export line otherwise
            out.println(next)
        }
    }
}